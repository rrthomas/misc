#!/usr/bin/env perl
# Do a regex search and replace on given files, or - for stdin
# (c) Reuben Thomas <rrt@sc3d.org> 20/10, 27/12/2006, 12/10/2007, 4/12/2008, 6, 14/8/2009, 7, 26/8/2010
# Released under the GPL version 3, or (at your option) any later version.

use strict;
use warnings;

use Perl6::Slurp;
use File::Basename;
use File::stat;

my $name = basename($0);

die "Usage: $name 's/FIND/REPLACE/[OPTS]' [FILE...]\n"
  if $#ARGV < 1;

my $op = "\$text =~ " . (shift) . "mg"; # FIXME: Don't auto-append "mg"?

push @ARGV, "-" if $#ARGV < 0;
foreach my $file (@ARGV) {
  my $text;
  if ($file eq "-") {
    $text = slurp \*STDIN or warn "$name: could not read `$file': $!\n";
  } else {
    if (!-e $file) {
      warn "$name: no such file `$file'\n";
      next;
    } elsif (!-f $file) {
      warn "$name: skipping non-regular file `$file'\n";
      next;
    }
    $text = slurp $file;
  }

  if (eval $op) {
    if ($file eq "-") {
      print $text;
    } else {
      my $stat = stat $file;
      system "mv", "-f", $file, "$file~";
      open FILE, ">$file" or warn "$name: could not write to `$file': $!\n";
      print FILE $text;
      close FILE;
      chmod $stat->mode, $file or warn "$name: $!";
      chown $stat->uid, $stat->gid, $file or warn "$name: $!";
      warn "$name: changed `$file'\n";
    }
  }
}
